apply plugin: 'com.android.application'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.chad.baserecyclerviewadapterhelper"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 5
        versionName "2.1"
    }

    signingConfigs {

        debugSignCfg {
            storeFile file(System.getenv("ECARX_PLATFORM_KEYSTORE") == null ? "/dev/null" : System.getenv("ECARX_PLATFORM_KEYSTORE"))
            storePassword System.getenv("ECARX_PLATFORM_KEYSTORE_PASSWORD")
            keyAlias System.getenv("ECARX_PLATFORM_KEY_ALIAS")
            keyPassword System.getenv("ECARX_PLATFORM_KEY_PASSWORD")
        }

        releaseSignCfg {
            storeFile file(System.getenv("ECARX_PLATFORM_KEYSTORE") == null ? "/dev/null" : System.getenv("ECARX_PLATFORM_KEYSTORE"))
            storePassword System.getenv("ECARX_PLATFORM_KEYSTORE_PASSWORD")
            keyAlias System.getenv("ECARX_PLATFORM_KEY_ALIAS")
            keyPassword System.getenv("ECARX_PLATFORM_KEY_PASSWORD")
        }

    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig localDebug() ? signingConfigs.debugSignCfg : signingConfigs.releaseSignCfg
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            signingConfig signingConfigs.debugSignCfg
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "flavor"

    productFlavors {

        beta {
            applicationId "com.chad.baserecyclerviewadapterhelper"
            dimension "flavor"
            manifestPlaceholders = [SHARED_USERID: applicationId]
        }

        // 如果需要写Setting ,那么需要系统签名
        beta_system {
            applicationId "com.chad.baserecyclerviewadapterhelper"
            dimension "flavor"
            manifestPlaceholders = [SHARED_USERID: "android.uid.system"]
        }

    }

    //支持Java8
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

boolean localDebug() {
    Properties properties = new Properties()
    File file = project.rootProject.file('local.properties')
    if (file == null || !file.exists()) {
        println "localDebug= null"
        return false
    }
    InputStream inputStream = file.newDataInputStream()
    properties.load(inputStream)
    def localDebugProperty = properties.getProperty('localDebug')
    def localDebug = localDebugProperty != null ? localDebugProperty.toBoolean() : false
    println "localDebug= " + localDebug
    return localDebug
}


dependencies {
    implementation fileTree(include: ['*.jar', '*.aar'], dir: 'libs')
    implementation project(path: ':library')
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:cardview-v7:28.0.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.kyleduo.switchbutton:library:2.0.0'
    implementation 'com.orhanobut:logger:2.2.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.squareup.picasso:picasso:2.3.2'
    implementation 'com.github.florent37:viewanimator:1.0.5'
    implementation 'com.android.support:support-v4:28.+'
}
